/* (c) https://github.com/MontiCore/monticore */
import de.monticore.MCTask

plugins {
    id "java"
    id "monticore"  version "$mc_version" // MontiCore Plugin
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    // useful reports
    // todo: reactivate this plugin when new version compatible to gradle 6.0.1 is available
//    id 'cz.malohlava.visteg' version '1.0.5'
    id "jacoco"
}


def guava_version = '30.0-jre'
def antlr_version = '4.7.1'
def junit_version = '4.13.2'
def logback_version = '1.1.2'

def grammarDir = 'src/main/grammars'

group = "tuhh.itl"
version = '1.1.3-SNAPSHOT'
description = "FDL DSL"
sourceCompatibility = "15"

// configure non-standard source sets
sourceSets {
  main.java.srcDirs += [ "$projectDir/target/generated-sources/monticore/sourcecode"]
}
buildDir = file("$projectDir/target")


dependencies {
  implementation "de.monticore:monticore-runtime:$mc_version"
  implementation "de.monticore:monticore-grammar:$mc_version"
  grammar ("de.monticore:monticore-grammar:$mc_version"){ capabilities {
            requireCapability("de.monticore:monticore-grammar-grammars")}
          }
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "de.se_rwth.commons:se-commons-utilities:$se_commons_version"
  implementation group:'com.google.guava', name:'guava', version:guava_version
  implementation group:'org.antlr', name:'antlr4-runtime', version:antlr_version
  implementation "de.monticore:monticore-generator:$mc_version"
  testImplementation group:'junit', name:'junit', version:junit_version
  implementation ("de.monticore:monticore-runtime:$mc_version"){ capabilities {
    requireCapability("de.monticore:monticore-runtime-tests")}
  }
  testImplementation group:'ch.qos.logback', name:'logback-classic', version:logback_version
  testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

// has to be placed directly under the dependency definition, since otherwise the grammar configurations are not found
repositories {
    mavenLocal()
    maven {
        url "https://nexus.se.rwth-aachen.de/content/groups/public"
    }
    // @Niklas Kersten darf sich später damit beschäftigen
    if(("true").equals(getProperty('useLocalRepo'))){
        mavenLocal()
    }
    maven {
        credentials.username mavenUser
        credentials.password mavenPassword
        url repo
    }
}



// one task per file
//fileTree(grammarDir).each {
//    def g = it
//    task  "generate${it.getName().substring(0,it.getName().lastIndexOf('.'))}" (type: MCTask) {
//        grammar = file g
//        outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
//        handcodedPath file("$projectDir/src/main/java")
//        modelPath file("$grammarDir")
//        def incCheckDir = file(grammarDir).toURI().relativize(g.toURI()).toString()
//        incCheckDir = incCheckDir.substring(0,incCheckDir.lastIndexOf('.')).toLowerCase()
//        def uptoDate = incCheck(outputDir.toString() +"/$incCheckDir/IncGenGradleCheck.txt")
//        outputs.upToDateWhen { uptoDate }
//    }
//}

task  generateMathExpressions (type: MCTask) {
    grammar = file "$grammarDir/fdl/MathExpressions.mc4"
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    def uptoDate = incCheck("MathExpressions.mc4")
    outputs.upToDateWhen { uptoDate }
}
task  generateAutomata (type: MCTask) {
    grammar = file "$grammarDir/fdl/Automata.mc4"
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    def uptoDate = incCheck("Automata.mc4")
    outputs.upToDateWhen { uptoDate }
}
task  generateFdl (type: MCTask) {
  grammar = file "$grammarDir/fdl/Fdl.mc4"
  outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
  def uptoDate = incCheck("Fdl.mc4")
  outputs.upToDateWhen { uptoDate }
}

generateFdl.dependsOn(generateMathExpressions)
generateFdl.dependsOn(generateAutomata)

compileJava {
    dependsOn project.collect { it.tasks.withType(MCTask) }
}

java {
  withSourcesJar()
  withJavadocJar()
}

sourcesJar.dependsOn project.collect { it.tasks.withType(MCTask)}

// generated java doc contains errors, disable for now
javadoc.failOnError(false)

// build grammar jar as well
task grammarJar(type: Jar) {
    from ("$projectDir/$grammarDir"){ include "**/*.mc4" }
    archiveClassifier = "grammars"
}

// configure deployment
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// todo: reactivate this plugin when new version compatible to gradle 6.0.1 is available
//visteg {
//    destination    = 'target/reports/visteg.dot'
//}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the 'test' task
    reportOn tasks.withType(Test)
}


task buildAll(type: GradleBuild) {
  tasks = [ 'build' ]
}

defaultTasks 'build'
targetCompatibility = JavaVersion.VERSION_15
